Import('env')


env = env.Clone(tools=['gresource'])

# SCons does not have recursive globs yet, use python glob module for now.
from glob import glob

modules = Flatten([
    glob('**/*.py', recursive=True),
    SConscript('features/SConscript', exports='env'),
    SConscript('fit/needle/SConscript', exports='env'),
    SConscript('fit/younglaplace/SConscript', exports='env'),
])

gresource_file = env.GResourceBundle('data.gresource', [
    env.GResource('/opendrop', glob('**/*.ui', recursive=True), compressed=True),
    env.GResource('/opendrop', glob('assets/**/*.png', recursive=True), compressed=False),
])

# Resolve all file paths now instead of returning strings.
package_files = env.File(modules + gresource_file)

Return('package_files')
